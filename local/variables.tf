variable "auth_hostname" {
  description = "Hostname for the default GoodData identity provider (Dex) ingress."
  type        = string
  default     = "localhost"
  validation {
    condition     = length(trimspace(var.auth_hostname)) > 0
    error_message = "auth_hostname must be provided."
  }
}

variable "deployment_name" {
  description = "Name prefix for local resources (and Helm releases)."
  type        = string
  default     = "gooddata-cn-local"
  validation {
    condition     = can(regex("^[a-z](?:[a-z0-9-]*[a-z0-9])?$", var.deployment_name))
    error_message = "deployment_name must be lowercase, start with a letter, contain only letters, numbers, and hyphens, and must not end with a hyphen."
  }
}

variable "dockerhub_access_token" {
  description = "Optional Docker Hub password/PAT used by k3d registry auth config."
  type        = string
  default     = ""
  sensitive   = true
}

variable "dockerhub_username" {
  description = "Optional Docker Hub username used by k3d registry auth config."
  type        = string
  default     = ""
  sensitive   = true
}

variable "enable_ai_features" {
  description = "Enable AI features in the gooddata-cn chart (GenAI service, semantic search, chat, metadata sync, and Qdrant)."
  type        = bool
  default     = true
}

variable "enable_experimental_features" {
  description = "Enable experimental AI features in the gooddata-cn chart. These are subject to change and the set of features may evolve over time."
  type        = bool
  default     = false
}

variable "enable_matomo_telemetry" {
  description = "Enable Matomo usage telemetry in the gooddata-cn chart."
  type        = bool
  default     = false
}

variable "enable_observability" {
  description = "Enable observability stack (Prometheus, Loki, Tempo, Grafana)"
  type        = bool
  default     = false
}

variable "gdcn_license_key" {
  description = "GoodData.CN license key (provided by your GoodData contact)."
  type        = string
  sensitive   = true
  validation {
    condition     = length(trimspace(var.gdcn_license_key)) > 0
    error_message = "gdcn_license_key must be provided."
  }
}

variable "gdcn_namespace" {
  description = "Kubernetes namespace used for GoodData.CN resources (and service account)."
  type        = string
  default     = "gooddata-cn"
  validation {
    condition     = length(trimspace(var.gdcn_namespace)) > 0
    error_message = "gdcn_namespace must be provided."
  }
}

variable "gdcn_orgs" {
  description = "Organizations to manage as Organization custom resources. If empty, Terraform does not create any Organization objects."
  type = list(object({
    admin_group = string
    admin_user  = string
    hostname    = string
    id          = string
    name        = string
  }))
  default = []

  validation {
    condition = (
      length(distinct([for org in var.gdcn_orgs : trimspace(org.id)])) == length(var.gdcn_orgs) &&
      length(distinct([for org in var.gdcn_orgs : trimspace(org.hostname)])) == length(var.gdcn_orgs)
      ) && alltrue([
        for org in var.gdcn_orgs : (
          length(trimspace(org.id)) > 0 &&
          can(regex("^[a-z0-9]([a-z0-9-]*[a-z0-9])?$", trimspace(org.id))) &&
          length(trimspace(org.name)) > 0 &&
          length(trimspace(org.admin_user)) > 0 &&
          length(trimspace(org.admin_group)) > 0 &&
          length(trimspace(org.hostname)) > 0
        )
    ])
    error_message = "gdcn_orgs must have unique non-empty ids (lowercase DNS labels) and hostnames, and each org must set non-empty name, admin_user, admin_group, and hostname."
  }
}

variable "helm_cert_manager_version" {
  description = "Version of the cert-manager Helm chart to deploy."
  type        = string
  # renovate: depName=cert-manager registryUrl=https://charts.jetstack.io
  default = "v1.21.1"
}

variable "helm_cnpg_version" {
  description = "Version of the CloudNativePG Helm chart to deploy."
  type        = string
  # renovate: depName=cloudnative-pg registryUrl=https://cloudnative-pg.github.io/charts
  default = "0.29.0"
}

variable "helm_gdcn_version" {
  description = "Version of the gooddata-cn Helm chart to deploy."
  type        = string

  validation {
    condition = (
      var.ingress_controller != "istio_gateway" ? true : (
        length(split(".", var.helm_gdcn_version)) >= 2 &&
        can(tonumber(split(".", var.helm_gdcn_version)[0])) &&
        can(tonumber(split(".", var.helm_gdcn_version)[1])) &&
        (
          tonumber(split(".", var.helm_gdcn_version)[0]) > 3 ||
          (
            tonumber(split(".", var.helm_gdcn_version)[0]) == 3 &&
            tonumber(split(".", var.helm_gdcn_version)[1]) >= 53
          )
        )
      )
    )
    error_message = "ingress_controller=\"istio_gateway\" requires helm_gdcn_version >= 3.53.0."
  }
}

variable "helm_grafana_version" {
  description = "Version of the grafana Helm chart to deploy."
  type        = string
  # renovate: depName=grafana registryUrl=https://grafana.github.io/helm-charts
  default = "10.5.15"
}

variable "helm_ingress_nginx_version" {
  description = "Version of the ingress-nginx Helm chart to deploy."
  type        = string
  # renovate: depName=ingress-nginx registryUrl=https://kubernetes.github.io/ingress-nginx
  default = "4.15.1"
}

variable "helm_istio_version" {
  description = "Version of the Istio Helm charts (base, istiod, gateway)."
  type        = string
  # renovate: depName=base registryUrl=https://istio-release.storage.googleapis.com/charts
  default = "1.30.3"
}

variable "helm_loki_version" {
  description = "Version of the loki Helm chart to deploy."
  type        = string
  # renovate: depName=loki registryUrl=https://grafana.github.io/helm-charts
  default = "7.3.0"
}

variable "helm_kube_prometheus_stack_version" {
  description = "Version of the kube-prometheus-stack Helm chart to deploy."
  type        = string
  # renovate: depName=kube-prometheus-stack registryUrl=https://prometheus-community.github.io/helm-charts
  default = "88.5.4"
}

variable "helm_prometheus_operator_crds_version" {
  description = "Version of the prometheus-operator-crds Helm chart. Must match the prometheus-operator version bundled in helm_kube_prometheus_stack_version."
  type        = string
  # renovate: depName=prometheus-operator-crds registryUrl=https://prometheus-community.github.io/helm-charts
  default = "31.0.1"
}

variable "helm_promtail_version" {
  description = "Version of the promtail Helm chart to deploy."
  type        = string
  # renovate: depName=promtail registryUrl=https://grafana.github.io/helm-charts
  default = "6.17.1"
}

variable "helm_pulsar_version" {
  description = "Version of the pulsar Helm chart to deploy."
  type        = string
  # renovate: depName=pulsar registryUrl=https://pulsar.apache.org/charts
  default = "4.7.0"
}

variable "helm_tempo_version" {
  description = "Version of the tempo Helm chart to deploy."
  type        = string
  # renovate: depName=tempo registryUrl=https://grafana.github.io/helm-charts
  default = "1.24.4"
}

variable "ingress_controller" {
  description = "Ingress controller used to expose GoodData.CN."
  type        = string
  default     = "ingress-nginx"
  validation {
    condition     = contains(["ingress-nginx", "istio_gateway"], var.ingress_controller)
    error_message = "For local deployments, ingress_controller must be one of: \"ingress-nginx\", \"istio_gateway\"."
  }
}

variable "ingress_nginx_behind_l7" {
  description = "Whether ingress-nginx is running behind an L7 proxy/load balancer (enables use-forwarded-headers)."
  type        = bool
  default     = false
}

variable "k3d_cluster_name" {
  description = "k3d cluster name to create/manage."
  type        = string
  default     = "gdcluster"
  validation {
    condition     = can(regex("^[a-z][a-z0-9-]*[a-z0-9]$", var.k3d_cluster_name))
    error_message = "k3d_cluster_name must start with a letter, contain only lowercase letters, numbers, and hyphens, and must not end with a hyphen."
  }
}

variable "k3d_kubeapi_host" {
  description = "Hostname to use in the generated kubeconfig for the k3d Kubernetes API server. Use host.docker.internal when running Terraform inside a container; use 127.0.0.1 on Linux hosts without host.docker.internal."
  type        = string
  default     = "host.docker.internal"
  validation {
    condition     = length(trimspace(var.k3d_kubeapi_host)) > 0
    error_message = "k3d_kubeapi_host must be provided."
  }
}

variable "kubeconfig_context" {
  description = "Kubeconfig context to use. If empty, defaults to k3d-<k3d_cluster_name>."
  type        = string
  default     = ""
}

variable "kubeconfig_path" {
  description = "Path to kubeconfig file to use (k3d updates this by default)."
  type        = string
  default     = "~/.kube/config"
}

variable "loki_retention_period" {
  description = "Loki log retention period (Go duration; multiple of 24h, e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "prometheus_retention_period" {
  description = "Prometheus metrics retention period (e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "tempo_retention_period" {
  description = "Tempo trace retention period (Go duration, e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "observability_hostname" {
  description = "Hostname for Grafana"
  type        = string
  default     = ""

  validation {
    condition     = var.enable_observability ? length(trimspace(var.observability_hostname)) > 0 : true
    error_message = "observability_hostname must be provided when enable_observability is true."
  }
}

variable "registry_dockerio" {
  description = "Container registry hostname used for images normally pulled from docker.io."
  type        = string
  default     = "docker.io"
}

variable "registry_k8sio" {
  description = "Container registry hostname used for images normally pulled from registry.k8s.io."
  type        = string
  default     = "registry.k8s.io"
}

variable "registry_quayio" {
  description = "Container registry hostname used for images normally pulled from quay.io."
  type        = string
  default     = "quay.io"
}

variable "seaweedfs_storage_size" {
  description = "Size of the SeaweedFS data PVC backing every local S3 bucket (Quiver cache, exports, datasource FS, Loki chunks, Tempo traces). Under local-path this request is not enforced as a quota — the node's disk is the real limit — and the PVC cannot be expanded in place, so raising this on an existing deployment means deleting and recreating it."
  type        = string
  default     = "50Gi"
}

variable "size_profile" {
  description = "Sizing profile for GoodData.CN and supporting services. Local installs only support \"dev\"."
  type        = string
  default     = "dev"
  validation {
    condition     = contains(["dev"], var.size_profile)
    error_message = "size_profile must be \"dev\" for local installs."
  }
}


variable "tls_mode" {
  description = "TLS management mode for local installs. Use selfsigned for cert-manager self-signed certificates."
  type        = string
  default     = "selfsigned"
  validation {
    condition     = contains(["selfsigned"], var.tls_mode)
    error_message = "tls_mode must be \"selfsigned\" for local installs."
  }
}

variable "gdcn_helm_extra_values" {
  description = "Additional Helm values YAML string appended to the gooddata-cn chart values. Use to override sub-chart settings not exposed as Terraform variables."
  type        = string
  default     = ""
}
