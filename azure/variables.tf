variable "aks_api_server_authorized_ip_ranges" {
  description = "List of CIDR ranges allowed to reach the AKS API server. Leave empty to allow Azure defaults."
  type        = list(string)
  default     = []
}

variable "aks_min_nodes" {
  description = "Minimum number of AKS worker nodes (sizes the fixed system node pool's node_count). If null, chosen by size_profile."
  type        = number
  default     = null
}

variable "aks_node_cpu_limit" {
  description = "Total vCPU ceiling for Karpenter/NAP-provisioned workload nodes, applied as the general NodePool's spec.limits.cpu. NAP stops provisioning once exceeded. If null, chosen by size_profile."
  type        = number
  default     = null
}

variable "aks_node_cpu_max" {
  description = "Per-node vCPU cap for Karpenter/NAP-provisioned workload nodes, applied as the general NodePool's karpenter.azure.com/sku-cpu Lt requirement. Bounds the size of any single workload node within the D family. If null, chosen by size_profile."
  type        = number
  default     = null
}

variable "aks_version" {
  description = "Version of AKS to deploy."
  type        = string
  default     = "1.36"
}

variable "auth_hostname" {
  description = "Hostname for the default GoodData identity provider (Dex) ingress."
  type        = string
  validation {
    condition     = length(trimspace(var.auth_hostname)) > 0
    error_message = "auth_hostname must be provided."
  }
}

variable "azure_additional_tags" {
  description = "Map of additional tags to apply to all Azure resources"
  type        = map(string)
  default     = {}
}

variable "azure_dns_zone_name" {
  description = "Name of the existing Azure DNS public zone used by external-dns when dns_provider = \"azure-dns\" (e.g. \"example.com\"). The zone must already exist in your subscription."
  type        = string
  default     = ""
  validation {
    condition     = var.dns_provider != "azure-dns" || length(trimspace(var.azure_dns_zone_name)) > 0
    error_message = "azure_dns_zone_name is required when dns_provider is \"azure-dns\"."
  }
}

variable "azure_dns_zone_resource_group_name" {
  description = "Name of the resource group that contains the Azure DNS zone referenced by azure_dns_zone_name. Required when dns_provider = \"azure-dns\"."
  type        = string
  default     = ""
  validation {
    condition     = var.dns_provider != "azure-dns" || length(trimspace(var.azure_dns_zone_resource_group_name)) > 0
    error_message = "azure_dns_zone_resource_group_name is required when dns_provider is \"azure-dns\"."
  }
}

variable "azure_location" {
  description = "Azure location to deploy resources to."
  type        = string
  default     = "East US"
}

variable "azure_storage_class" {
  description = "Kubernetes StorageClass for GoodData.CN chart PVCs. If null, chosen by size_profile (standard for dev, premium for prod)."
  type        = string
  default     = null
}

variable "azure_subscription_id" {
  description = "Azure subscription ID to deploy resources to. If not set, uses current az login subscription."
  type        = string
  default     = null
}

variable "azure_tenant_id" {
  description = "Azure tenant ID for authentication. If not set, uses current az login tenant."
  type        = string
  default     = null
}

variable "deployment_name" {
  description = "Name prefix for all Azure resources."
  type        = string
  default     = "gooddata-cn"

  validation {
    condition     = can(regex("^[a-z0-9-]{1,50}$", var.deployment_name)) && length(join("", regexall("[0-9a-z]", lower(var.deployment_name)))) <= 18
    error_message = "The deployment_name must contain only lowercase letters, numbers, and hyphens. After removing non-alphanumeric characters, it must be ≤18 characters to allow space for 6-character random suffix."
  }
}

variable "dns_provider" {
  description = "DNS management mode on Azure. \"self-managed\" leaves DNS to the user; \"azure-dns\" deploys external-dns wired to an existing Azure DNS zone and automatically maintains A records for each GoodData hostname."
  type        = string
  default     = "self-managed"
  validation {
    condition     = contains(["self-managed", "azure-dns"], var.dns_provider)
    error_message = "dns_provider must be \"self-managed\" or \"azure-dns\"."
  }
}

variable "dockerhub_access_token" {
  description = "Docker Hub access token (can be created in Settings > Personal Access Tokens)"
  type        = string
  default     = ""
  sensitive   = true
  validation {
    condition     = var.enable_image_cache ? length(var.dockerhub_access_token) > 0 : true
    error_message = "dockerhub_access_token must be provided when enable_image_cache is true."
  }
}

variable "dockerhub_username" {
  description = "Docker Hub username (used to pull images without hitting rate limits). Free account is enough."
  type        = string
  default     = ""
  sensitive   = true
  validation {
    condition     = var.enable_image_cache ? length(var.dockerhub_username) > 0 : true
    error_message = "dockerhub_username must be provided when enable_image_cache is true."
  }
}

variable "enable_ai_features" {
  description = "Enable AI features in the gooddata-cn chart (GenAI service, semantic search, chat, metadata sync, and Qdrant)."
  type        = bool
  default     = true
}

variable "enable_experimental_features" {
  description = "Enable experimental AI features in the gooddata-cn chart. These are subject to change and the set of features may evolve over time."
  type        = bool
  default     = false
}

variable "enable_matomo_telemetry" {
  description = "Enable Matomo usage telemetry in the gooddata-cn chart."
  type        = bool
  default     = false
}

variable "enable_image_cache" {
  description = "Enable image caching (ACR pull-through cache). If false, images are pulled from upstream registries directly."
  type        = bool
  default     = false
}

variable "enable_observability" {
  description = "Enable observability stack (Prometheus, Loki, Tempo, Grafana)"
  type        = bool
  default     = false
}

variable "gdcn_helm_extra_values" {
  description = "Additional Helm values YAML string appended to the gooddata-cn chart values. Use to override sub-chart settings not exposed as Terraform variables."
  type        = string
  default     = ""
}

variable "gdcn_license_key" {
  description = "GoodData.CN license key (provided by your GoodData contact)"
  type        = string
  sensitive   = true
}

variable "gdcn_namespace" {
  description = "Kubernetes namespace used for GoodData.CN resources (and service account)."
  type        = string
  default     = "gooddata-cn"
  validation {
    condition     = length(trimspace(var.gdcn_namespace)) > 0
    error_message = "gdcn_namespace must be provided."
  }
}

variable "gdcn_orgs" {
  description = "Organizations to manage as Organization custom resources. If empty, Terraform does not create any Organization objects."
  type = list(object({
    admin_group = string
    admin_user  = string
    hostname    = string
    id          = string
    name        = string
  }))
  default = []

  validation {
    condition = (
      length(distinct([for org in var.gdcn_orgs : trimspace(org.id)])) == length(var.gdcn_orgs) &&
      length(distinct([for org in var.gdcn_orgs : trimspace(org.hostname)])) == length(var.gdcn_orgs)
      ) && alltrue([
        for org in var.gdcn_orgs : (
          length(trimspace(org.id)) > 0 &&
          can(regex("^[a-z0-9]([a-z0-9-]*[a-z0-9])?$", trimspace(org.id))) &&
          length(trimspace(org.name)) > 0 &&
          length(trimspace(org.admin_user)) > 0 &&
          length(trimspace(org.admin_group)) > 0 &&
          length(trimspace(org.hostname)) > 0
        )
    ])
    error_message = "gdcn_orgs must have unique non-empty ids (lowercase DNS labels) and hostnames, and each org must set non-empty name, admin_user, admin_group, and hostname."
  }
}

variable "helm_cert_manager_version" {
  description = "Version of the cert-manager Helm chart to deploy. https://artifacthub.io/packages/helm/cert-manager/cert-manager"
  type        = string
  # renovate: depName=cert-manager registryUrl=https://charts.jetstack.io
  default = "v1.21.1"
}

variable "helm_external_dns_version" {
  description = "Version of the external-dns Helm chart to deploy. https://artifacthub.io/packages/helm/external-dns/external-dns"
  type        = string
  # renovate: depName=external-dns registryUrl=https://kubernetes-sigs.github.io/external-dns/
  default = "1.21.1"
}

variable "helm_gdcn_version" {
  description = "Version of the gooddata-cn Helm chart to deploy. https://artifacthub.io/packages/helm/gooddata-cn/gooddata-cn"
  type        = string

  validation {
    condition = (
      var.ingress_controller != "istio_gateway" ? true : (
        length(split(".", var.helm_gdcn_version)) >= 2 &&
        can(tonumber(split(".", var.helm_gdcn_version)[0])) &&
        can(tonumber(split(".", var.helm_gdcn_version)[1])) &&
        (
          tonumber(split(".", var.helm_gdcn_version)[0]) > 3 ||
          (
            tonumber(split(".", var.helm_gdcn_version)[0]) == 3 &&
            tonumber(split(".", var.helm_gdcn_version)[1]) >= 53
          )
        )
      )
    )
    error_message = "ingress_controller=\"istio_gateway\" requires helm_gdcn_version >= 3.53.0."
  }
}

variable "helm_grafana_version" {
  description = "Version of the grafana Helm chart to deploy. https://artifacthub.io/packages/helm/grafana/grafana"
  type        = string
  # renovate: depName=grafana registryUrl=https://grafana.github.io/helm-charts
  default = "10.5.15"
}

variable "helm_ingress_nginx_version" {
  description = "Version of the ingress-nginx Helm chart to deploy. https://artifacthub.io/packages/helm/ingress-nginx/ingress-nginx"
  type        = string
  # renovate: depName=ingress-nginx registryUrl=https://kubernetes.github.io/ingress-nginx
  default = "4.15.1"
}

variable "helm_istio_version" {
  description = "Version of the Istio Helm charts (base, istiod, gateway). https://istio.io/latest/docs/setup/install/helm/"
  type        = string
  # renovate: depName=base registryUrl=https://istio-release.storage.googleapis.com/charts
  default = "1.30.3"
}

variable "helm_loki_version" {
  description = "Version of the loki Helm chart to deploy. https://artifacthub.io/packages/helm/grafana/loki"
  type        = string
  # renovate: depName=loki registryUrl=https://grafana.github.io/helm-charts
  default = "7.3.0"
}

variable "helm_kube_prometheus_stack_version" {
  description = "Version of the kube-prometheus-stack Helm chart to deploy. https://artifacthub.io/packages/helm/prometheus-community/kube-prometheus-stack"
  type        = string
  # renovate: depName=kube-prometheus-stack registryUrl=https://prometheus-community.github.io/helm-charts
  default = "88.5.4"
}

variable "helm_promtail_version" {
  description = "Version of the promtail Helm chart to deploy. https://artifacthub.io/packages/helm/grafana/promtail"
  type        = string
  # renovate: depName=promtail registryUrl=https://grafana.github.io/helm-charts
  default = "6.17.1"
}

variable "helm_pulsar_version" {
  description = "Version of the pulsar Helm chart to deploy. https://artifacthub.io/packages/helm/apache/pulsar"
  type        = string
  # renovate: depName=pulsar registryUrl=https://pulsar.apache.org/charts
  default = "4.7.0"
}

variable "helm_tempo_version" {
  description = "Version of the tempo Helm chart to deploy. https://artifacthub.io/packages/helm/grafana/tempo"
  type        = string
  # renovate: depName=tempo registryUrl=https://grafana.github.io/helm-charts
  default = "1.24.4"
}

variable "ingress_controller" {
  description = "Ingress controller to deploy. Use ingress-nginx for Kubernetes Ingress, or istio_gateway to expose the Istio ingress gateway via LoadBalancer."
  type        = string
  default     = "ingress-nginx"

  validation {
    condition     = contains(["ingress-nginx", "istio_gateway"], var.ingress_controller)
    error_message = "ingress_controller must be one of: \"ingress-nginx\", \"istio_gateway\"."
  }
}

variable "ingress_nginx_behind_l7" {
  description = "Whether ingress-nginx is running behind an L7 proxy/load balancer (enables use-forwarded-headers)."
  type        = bool
  default     = false
}

variable "letsencrypt_email" {
  description = "Email address used for Let's Encrypt ACME registration (only required when tls_mode = \"letsencrypt\")"
  type        = string
  default     = ""
  validation {
    condition     = var.tls_mode != "letsencrypt" ? true : length(trimspace(var.letsencrypt_email)) > 0
    error_message = "letsencrypt_email must be provided when tls_mode is \"letsencrypt\"."
  }
}

variable "loki_retention_period" {
  description = "Loki log retention period (Go duration; multiple of 24h, e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "prometheus_retention_period" {
  description = "Prometheus metrics retention period (e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "tempo_retention_period" {
  description = "Tempo trace retention period (Go duration, e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "observability_hostname" {
  description = "Hostname for Grafana"
  type        = string
  default     = ""

  validation {
    condition     = var.enable_observability ? length(trimspace(var.observability_hostname)) > 0 : true
    error_message = "observability_hostname must be provided when enable_observability is true."
  }
}

variable "postgresql_backup_retention_days" {
  description = "Days of automated PostgreSQL backups / point-in-time recovery. If null, chosen by size_profile (14 for prod, 7 for dev)."
  type        = number
  default     = null
  validation {
    # coalesce keeps floor() off a null value: Terraform before 1.12
    # evaluates both sides of ||.
    condition = (
      coalesce(var.postgresql_backup_retention_days, 7) == floor(coalesce(var.postgresql_backup_retention_days, 7))
      && coalesce(var.postgresql_backup_retention_days, 7) >= 7
      && coalesce(var.postgresql_backup_retention_days, 7) <= 35
    )
    error_message = "postgresql_backup_retention_days must be a whole number of days between 7 and 35."
  }
}

variable "postgresql_geo_redundant_backup" {
  description = "Replicate PostgreSQL backups to the Azure paired region. Create-time only: changing it on an existing server destroys and recreates the database."
  type        = bool
  default     = false
}

variable "postgresql_storage_mb" {
  description = "Azure Database for PostgreSQL storage in MB. If null, chosen by size_profile. Set this if auto_grow_enabled has raised the disk above the profile value, since Azure rejects storage decreases."
  type        = number
  default     = null
}

variable "postgresql_sku_name" {
  description = "Azure Database for PostgreSQL SKU name. If null, chosen by size_profile. E.g. B_Standard_B1ms, GP_Standard_D2s_v3, MO_Standard_E4s_v3"
  type        = string
  default     = null
}


variable "size_profile" {
  description = "Sizing profile for GoodData.CN and supporting services."
  type        = string
  default     = "prod-small"
  validation {
    condition     = contains(["dev", "prod-small", "prod-large"], var.size_profile)
    error_message = "size_profile must be one of: dev, prod-small, prod-large."
  }
}

variable "tls_mode" {
  description = "TLS management mode. Use letsencrypt for Let's Encrypt certificates."
  type        = string
  default     = "letsencrypt"
  validation {
    condition     = var.tls_mode == "letsencrypt"
    error_message = "tls_mode must be \"letsencrypt\" on Azure."
  }
}
