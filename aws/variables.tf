variable "ai_lake_size_profile" {
  description = "AI Lake sizing profile. Required when enable_ai_lake is true; one of: dev, prod-small, prod-xl. Not derived from size_profile."
  type        = string
  default     = null
  validation {
    condition     = var.ai_lake_size_profile == null || contains(["dev", "prod-small", "prod-xl"], var.ai_lake_size_profile)
    error_message = "ai_lake_size_profile must be one of: dev, prod-small, prod-xl."
  }
  validation {
    condition     = !var.enable_ai_lake || var.ai_lake_size_profile != null
    error_message = "ai_lake_size_profile must be set when enable_ai_lake is true."
  }
}

variable "auth_hostname" {
  description = "Hostname for the default GoodData identity provider (Dex) ingress."
  type        = string
  validation {
    condition     = length(trimspace(var.auth_hostname)) > 0
    error_message = "auth_hostname must be provided."
  }
}

variable "aws_additional_tags" {
  description = "Map of additional tags to apply to all AWS resources"
  type        = map(string)
  default     = {}
}

variable "aws_profile_name" {
  description = "Name of AWS profile defined in ~/.aws/config to be used by Terraform."
  type        = string
}

variable "aws_region" {
  description = "AWS region to deploy resources to."
  type        = string
  default     = "us-east-2"
}

variable "deployment_name" {
  description = "Name prefix for all AWS resources."
  type        = string
  default     = "gooddata-cn"
  validation {
    condition     = can(regex("^[a-z](?:[a-z0-9-]*[a-z0-9])?$", var.deployment_name))
    error_message = "deployment_name must be lowercase, start with a letter, contain only letters, numbers, and hyphens, and must not end with a hyphen."
  }
}

variable "dns_provider" {
  description = "DNS management mode on AWS. Use route53 to enable ExternalDNS, or self-managed to manage DNS yourself."
  type        = string
  default     = "self-managed"
  validation {
    condition     = contains(["route53", "self-managed"], var.dns_provider)
    error_message = "dns_provider must be \"route53\" or \"self-managed\"."
  }
}

variable "dockerhub_access_token" {
  description = "Docker Hub access token (can be created in Settings > Personal Access Tokens)"
  type        = string
  default     = ""
  sensitive   = true
  validation {
    condition     = var.enable_image_cache ? length(var.dockerhub_access_token) > 0 : true
    error_message = "dockerhub_access_token must be provided when enable_image_cache is true."
  }
}

variable "dockerhub_username" {
  description = "Docker Hub username (used to pull images without hitting rate limits). Free account is enough."
  type        = string
  default     = ""
  sensitive   = true
  validation {
    condition     = var.enable_image_cache ? length(var.dockerhub_username) > 0 : true
    error_message = "dockerhub_username must be provided when enable_image_cache is true."
  }
}

variable "eks_ai_lake_node_types" {
  description = "EC2 instance types for the dedicated AI Lake node pool (taint workload=starrocks). If null, defaults to a preset chosen by ai_lake_size_profile."
  type        = list(string)
  default     = null
}

variable "eks_endpoint_private_access" {
  description = "Whether the EKS API server is reachable privately from within the VPC."
  type        = bool
  default     = false
}

variable "eks_endpoint_public_access" {
  description = "Whether the EKS API server is reachable publicly."
  type        = bool
  default     = true
}

variable "eks_endpoint_public_access_cidrs" {
  description = "CIDR blocks allowed to reach the public EKS endpoint when enabled."
  type        = list(string)
  default     = ["0.0.0.0/0"]
  validation {
    condition     = var.eks_endpoint_public_access ? length(var.eks_endpoint_public_access_cidrs) > 0 : true
    error_message = "Provide at least one CIDR when eks_endpoint_public_access is true."
  }
}

variable "eks_node_cpu_limit" {
  description = "Total vCPU ceiling for Karpenter-provisioned general workload nodes, applied as the general NodePool's spec.limits.cpu. Karpenter stops provisioning once exceeded. If null, chosen by size_profile."
  type        = number
  default     = null
}

variable "eks_node_cpu_max" {
  description = "Per-node vCPU cap for Karpenter-provisioned general workload nodes, applied as the general NodePool's karpenter.k8s.aws/instance-cpu Lt requirement. Bounds the size of any single workload node within the m category. If null, chosen by size_profile."
  type        = number
  default     = null
}

variable "eks_node_disk_size" {
  description = "Data volume size in GiB for every node's /dev/xvdb, on both the system node group and the Karpenter EC2NodeClass. Bottlerocket otherwise defaults to 20 GiB, which is below the ephemeral-storage some GoodData.CN pods request and leaves them unschedulable on every instance type."
  type        = number
  default     = 100
}

variable "eks_node_max_pods" {
  description = "Kubelet maxPods for every node, set on the Karpenter EC2NodeClass and in the system node group's Bottlerocket settings. Both otherwise derive it from the instance type's secondary-IP limits, which leaves VPC CNI prefix delegation inert. 110 is the AWS-recommended ceiling for instances under 30 vCPU."
  type        = number
  default     = 110
}

variable "eks_storage_class" {
  description = "Kubernetes StorageClass for GoodData.CN chart PVCs. If null, chosen by size_profile. Latency-sensitive PVCs use the fast class instead, which is not user-configurable."
  type        = string
  default     = null
}

variable "eks_version" {
  description = "Version of EKS to deploy."
  type        = string
  default     = "1.35"
}

variable "enable_ai_features" {
  description = "Enable AI features in the gooddata-cn chart (GenAI service, semantic search, chat, metadata sync, and Qdrant)."
  type        = bool
  default     = true
}

variable "enable_experimental_features" {
  description = "Enable experimental AI features in the gooddata-cn chart. These are subject to change and the set of features may evolve over time."
  type        = bool
  default     = false
}

variable "enable_matomo_telemetry" {
  description = "Enable Matomo usage telemetry in the gooddata-cn chart."
  type        = bool
  default     = false
}

variable "enable_image_cache" {
  description = "Enable image caching (ECR pull-through cache). If false, images are pulled from upstream registries directly."
  type        = bool
  default     = false
}

variable "enable_observability" {
  description = "Enable observability stack (Prometheus, Loki, Tempo, Grafana)"
  type        = bool
  default     = false
}

variable "enable_ai_lake" {
  description = "Enable AI Lake deployment for analytics query acceleration"
  type        = bool
  default     = false
}

variable "existing_private_subnet_ids" {
  description = "Private subnet IDs in the existing VPC (for EKS nodes and RDS). Required when existing_vpc_id is set. Must span at least 2 AZs."
  type        = list(string)
  default     = []
  validation {
    condition     = length(var.existing_private_subnet_ids) == 0 || length(var.existing_private_subnet_ids) >= 2
    error_message = "existing_private_subnet_ids must be empty or contain at least 2 entries."
  }
}

variable "existing_public_subnet_ids" {
  description = "Public subnet IDs in the existing VPC (for load balancers). Required when existing_vpc_id is set. Must span at least 2 AZs. Subnets must be tagged with kubernetes.io/role/elb=1 and kubernetes.io/cluster/<deployment_name>=shared for the AWS Load Balancer Controller."
  type        = list(string)
  default     = []
  validation {
    condition     = length(var.existing_public_subnet_ids) == 0 || length(var.existing_public_subnet_ids) >= 2
    error_message = "existing_public_subnet_ids must be empty or contain at least 2 entries."
  }
}

variable "existing_vpc_id" {
  description = "ID of an existing VPC to deploy into. Leave empty to create a new VPC. When set, existing_private_subnet_ids and existing_public_subnet_ids must also be provided."
  type        = string
  default     = ""
}

variable "gdcn_helm_extra_values" {
  description = "Additional Helm values YAML string appended to the gooddata-cn chart values. Use to override sub-chart settings not exposed as Terraform variables."
  type        = string
  default     = ""
}

variable "gdcn_license_key" {
  description = "GoodData.CN license key (provided by your GoodData contact)"
  type        = string
  sensitive   = true
}

variable "gdcn_namespace" {
  description = "Kubernetes namespace used for GoodData.CN resources (and service account)."
  type        = string
  default     = "gooddata-cn"
  validation {
    condition     = length(trimspace(var.gdcn_namespace)) > 0
    error_message = "gdcn_namespace must be provided."
  }
}

variable "gdcn_orgs" {
  description = "Organizations to manage as Organization custom resources. If empty, Terraform does not create any Organization objects."
  type = list(object({
    admin_group = string
    admin_user  = string
    hostname    = string
    id          = string
    name        = string
  }))
  default = []

  validation {
    condition = (
      length(distinct([for org in var.gdcn_orgs : trimspace(org.id)])) == length(var.gdcn_orgs) &&
      length(distinct([for org in var.gdcn_orgs : trimspace(org.hostname)])) == length(var.gdcn_orgs)
      ) && alltrue([
        for org in var.gdcn_orgs : (
          length(trimspace(org.id)) > 0 &&
          can(regex("^[a-z0-9]([a-z0-9-]*[a-z0-9])?$", trimspace(org.id))) &&
          length(trimspace(org.name)) > 0 &&
          length(trimspace(org.admin_user)) > 0 &&
          length(trimspace(org.admin_group)) > 0 &&
          length(trimspace(org.hostname)) > 0
        )
    ])
    error_message = "gdcn_orgs must have unique non-empty ids (lowercase DNS labels) and hostnames, and each org must set non-empty name, admin_user, admin_group, and hostname."
  }
}

variable "helm_aws_lb_controller_version" {
  description = "Version of the aws-load-balancer-controller Helm chart to deploy. https://artifacthub.io/packages/helm/aws/aws-load-balancer-controller"
  type        = string
  # renovate: depName=aws-load-balancer-controller registryUrl=https://aws.github.io/eks-charts
  default = "3.5.0"
}

variable "helm_cert_manager_version" {
  description = "Version of the cert-manager Helm chart to deploy. https://artifacthub.io/packages/helm/cert-manager/cert-manager"
  type        = string
  # renovate: depName=cert-manager registryUrl=https://charts.jetstack.io
  default = "v1.21.1"
}

variable "helm_external_dns_version" {
  description = "Version of the external-dns Helm chart to deploy. https://artifacthub.io/packages/helm/external-dns/external-dns"
  type        = string
  # renovate: depName=external-dns registryUrl=https://kubernetes-sigs.github.io/external-dns/
  default = "1.21.1"
}

variable "helm_gdcn_version" {
  description = "Version of the gooddata-cn Helm chart to deploy. https://artifacthub.io/packages/helm/gooddata-cn/gooddata-cn"
  type        = string
  validation {
    condition = (
      # ALB support requires GoodData chart features introduced in 3.51+
      (var.ingress_controller != "alb" ? true : (
        length(split(".", var.helm_gdcn_version)) >= 2 &&
        can(tonumber(split(".", var.helm_gdcn_version)[0])) &&
        can(tonumber(split(".", var.helm_gdcn_version)[1])) &&
        (
          tonumber(split(".", var.helm_gdcn_version)[0]) > 3 ||
          (
            tonumber(split(".", var.helm_gdcn_version)[0]) == 3 &&
            tonumber(split(".", var.helm_gdcn_version)[1]) >= 51
          )
        )
      )) &&
      # Istio existingGateway support (incl Dex) requires GoodData chart features introduced in 3.53+
      (var.ingress_controller != "istio_gateway" ? true : (
        length(split(".", var.helm_gdcn_version)) >= 2 &&
        can(tonumber(split(".", var.helm_gdcn_version)[0])) &&
        can(tonumber(split(".", var.helm_gdcn_version)[1])) &&
        (
          tonumber(split(".", var.helm_gdcn_version)[0]) > 3 ||
          (
            tonumber(split(".", var.helm_gdcn_version)[0]) == 3 &&
            tonumber(split(".", var.helm_gdcn_version)[1]) >= 53
          )
        )
      ))
    )
    error_message = "Invalid helm_gdcn_version for selected features. ingress_controller=\"alb\" requires helm_gdcn_version >= 3.51.0. ingress_controller=\"istio_gateway\" requires helm_gdcn_version >= 3.53.0."
  }
}

variable "helm_grafana_version" {
  description = "Version of the grafana Helm chart to deploy. https://artifacthub.io/packages/helm/grafana/grafana"
  type        = string
  # renovate: depName=grafana registryUrl=https://grafana.github.io/helm-charts
  default = "10.5.15"
}

variable "helm_ingress_nginx_version" {
  description = "Version of the ingress-nginx Helm chart to deploy. https://artifacthub.io/packages/helm/ingress-nginx/ingress-nginx"
  type        = string
  # renovate: depName=ingress-nginx registryUrl=https://kubernetes.github.io/ingress-nginx
  default = "4.15.1"
}

variable "helm_istio_version" {
  description = "Version of the Istio Helm charts (base, istiod, gateway). https://istio.io/latest/docs/setup/install/helm/"
  type        = string
  # renovate: depName=base registryUrl=https://istio-release.storage.googleapis.com/charts
  default = "1.30.3"
}

variable "helm_karpenter_version" {
  description = "Version of the Karpenter Helm chart (oci://public.ecr.aws/karpenter/karpenter) to deploy. https://github.com/aws/karpenter-provider-aws/releases"
  type        = string
  # renovate: depName=karpenter packageName=public.ecr.aws/karpenter/karpenter datasource=docker
  default = "1.6.3"
}

variable "helm_loki_version" {
  description = "Version of the loki Helm chart to deploy. https://artifacthub.io/packages/helm/grafana/loki"
  type        = string
  # renovate: depName=loki registryUrl=https://grafana.github.io/helm-charts
  default = "7.3.0"
}

variable "helm_metrics_server_version" {
  description = "Version of the metrics-server Helm chart to deploy. https://artifacthub.io/packages/helm/metrics-server/metrics-server"
  type        = string
  # renovate: depName=metrics-server registryUrl=https://kubernetes-sigs.github.io/metrics-server/
  default = "3.14.0"
}

variable "helm_kube_prometheus_stack_version" {
  description = "Version of the kube-prometheus-stack Helm chart to deploy. https://artifacthub.io/packages/helm/prometheus-community/kube-prometheus-stack"
  type        = string
  # renovate: depName=kube-prometheus-stack registryUrl=https://prometheus-community.github.io/helm-charts
  default = "88.5.4"
}

variable "helm_promtail_version" {
  description = "Version of the promtail Helm chart to deploy. https://artifacthub.io/packages/helm/grafana/promtail"
  type        = string
  # renovate: depName=promtail registryUrl=https://grafana.github.io/helm-charts
  default = "6.17.1"
}

variable "helm_pulsar_version" {
  description = "Version of the pulsar Helm chart to deploy. https://artifacthub.io/packages/helm/apache/pulsar"
  type        = string
  # renovate: depName=pulsar registryUrl=https://pulsar.apache.org/charts
  default = "4.7.0"
}

variable "helm_starrocks_version" {
  description = "Version of the kube-starrocks Helm chart to deploy. https://artifacthub.io/packages/helm/kube-starrocks/kube-starrocks"
  type        = string
  # renovate: depName=kube-starrocks registryUrl=https://starrocks.github.io/starrocks-kubernetes-operator
  default = "1.11.7"
}

variable "helm_tempo_version" {
  description = "Version of the tempo Helm chart to deploy. https://artifacthub.io/packages/helm/grafana/tempo"
  type        = string
  # renovate: depName=tempo registryUrl=https://grafana.github.io/helm-charts
  default = "1.24.4"
}

variable "ingress_controller" {
  description = "Ingress controller used to expose GoodData.CN. Use alb for AWS ALB, ingress-nginx for Kubernetes Ingress, or istio_gateway to expose the Istio ingress gateway via LoadBalancer."
  type        = string
  default     = "alb"
  validation {
    condition = (
      contains(["ingress-nginx", "alb", "istio_gateway"], var.ingress_controller)
    )
    error_message = "ingress_controller must be one of: \"alb\", \"ingress-nginx\", \"istio_gateway\"."
  }
}

variable "ingress_nginx_behind_l7" {
  description = "Whether ingress-nginx is running behind an L7 proxy/load balancer (enables use-forwarded-headers)."
  type        = bool
  default     = false
}

variable "letsencrypt_email" {
  description = "Email address used for Let's Encrypt ACME registration (only required when tls_mode = \"letsencrypt\")"
  type        = string
  default     = ""
  validation {
    condition     = var.tls_mode != "letsencrypt" ? true : length(trimspace(var.letsencrypt_email)) > 0
    error_message = "letsencrypt_email must be provided when tls_mode is \"letsencrypt\"."
  }
}

variable "loki_retention_period" {
  description = "Loki log retention period (Go duration; multiple of 24h, e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "prometheus_retention_period" {
  description = "Prometheus metrics retention period (e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "tempo_retention_period" {
  description = "Tempo trace retention period (Go duration, e.g. 168h = 7 days)."
  type        = string
  default     = "168h"
}

variable "observability_hostname" {
  description = "Hostname for Grafana"
  type        = string
  default     = ""

  validation {
    condition     = var.enable_observability ? length(trimspace(var.observability_hostname)) > 0 : true
    error_message = "observability_hostname must be provided when enable_observability is true."
  }
}

variable "rds_allocated_storage" {
  description = "RDS PostgreSQL allocated storage in GB. If null, chosen by size_profile."
  type        = number
  default     = null
}

variable "rds_allow_major_version_upgrade" {
  description = "Allow RDS engine major version upgrades when engine_version changes across major versions."
  type        = bool
  default     = false
}

variable "rds_apply_immediately" {
  description = "Apply RDS modifications during terraform apply instead of the next maintenance window. Instance-class changes reboot the database, so leave this off unless you accept downtime."
  type        = bool
  default     = false
}

variable "rds_backup_retention_period" {
  description = "Days of automated RDS backups / point-in-time recovery. If null, chosen by size_profile (14 for prod, 7 for dev)."
  type        = number
  default     = null
  validation {
    # coalesce keeps floor() off a null value: Terraform before 1.12
    # evaluates both sides of ||.
    condition = (
      coalesce(var.rds_backup_retention_period, 0) == floor(coalesce(var.rds_backup_retention_period, 0))
      && coalesce(var.rds_backup_retention_period, 0) >= 0
      && coalesce(var.rds_backup_retention_period, 0) <= 35
    )
    error_message = "rds_backup_retention_period must be a whole number of days between 0 and 35."
  }
}

variable "rds_deletion_protection" {
  description = "Enable deletion protection on the RDS instance. If null, chosen by size_profile (on for prod, off for dev)."
  type        = bool
  default     = null
}

variable "rds_instance_class" {
  description = "RDS PostgreSQL instance class. If null, chosen by size_profile."
  type        = string
  default     = null
}

variable "rds_skip_final_snapshot" {
  description = "Skip taking a final snapshot when destroying the RDS instance. If null, chosen by size_profile (final snapshot for prod, skipped for dev)."
  type        = bool
  default     = null
}

variable "route53_zone_id" {
  description = "Route53 hosted zone ID used for DNS and ACM validation when dns_provider = \"route53\"."
  type        = string
  default     = ""
  validation {
    condition     = var.dns_provider != "route53" ? true : length(trimspace(var.route53_zone_id)) > 0
    error_message = "route53_zone_id is required when dns_provider is \"route53\"."
  }
}

variable "size_profile" {
  description = "Sizing profile for GoodData.CN and supporting services."
  type        = string
  default     = "prod-small"
  validation {
    condition     = contains(["dev", "prod-small", "prod-xl", "prod-large"], var.size_profile)
    error_message = "size_profile must be one of: dev, prod-small, prod-xl, prod-large."
  }
}

variable "starrocks_cn_image_tag" {
  description = "Docker image tag for StarRocks CN nodes"
  type        = string
  default     = "4.0.6-20260507-091022-3c12ee9"
}

variable "starrocks_fe_image_tag" {
  description = "Docker image tag for StarRocks FE nodes"
  type        = string
  default     = "4.0.6-20260507-091022-3c12ee9"
}

variable "tls_mode" {
  description = "TLS management mode. Use acm for ALB, letsencrypt for ingress-nginx."
  type        = string
  default     = "acm"
  validation {
    condition = (
      contains(["acm", "letsencrypt"], var.tls_mode) &&
      (var.tls_mode != "acm" ? true : var.ingress_controller == "alb") &&
      (var.tls_mode != "letsencrypt" ? true : contains(["ingress-nginx", "istio_gateway"], var.ingress_controller))
    )
    error_message = "tls_mode=\"acm\" requires ingress_controller=\"alb\"; tls_mode=\"letsencrypt\" requires ingress_controller=\"ingress-nginx\" or \"istio_gateway\"."
  }
}
